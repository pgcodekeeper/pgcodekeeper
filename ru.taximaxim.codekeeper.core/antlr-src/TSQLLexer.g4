lexer grammar TSQLLexer;

options {
    caseInsensitive = true;
}

@header {package ru.taximaxim.codekeeper.core.parsers.antlr.generated;}

// Basic keywords (from https://msdn.microsoft.com/en-us/library/ms189822.aspx)
ADD: 'ADD';
ALL: 'ALL';
ALTER: 'ALTER';
AND: 'AND';
ANY: 'ANY';
AS: 'AS';
ASC: 'ASC';
AUTHORIZATION: 'AUTHORIZATION';
BACKUP: 'BACKUP';
BEGIN: 'BEGIN';
BETWEEN: 'BETWEEN';
BREAK: 'BREAK';
BROWSE: 'BROWSE';
BULK: 'BULK';
BY: 'BY';
CASCADE: 'CASCADE';
CASE: 'CASE';
CHECK: 'CHECK';
CHECKPOINT: 'CHECKPOINT';
CLOSE: 'CLOSE';
CLUSTERED: 'CLUSTERED';
COALESCE: 'COALESCE';
COLLATE: 'COLLATE';
COLUMN: 'COLUMN';
COMMIT: 'COMMIT';
COMPUTE: 'COMPUTE';
CONSTRAINT: 'CONSTRAINT';
CONTAINS: 'CONTAINS';
CONTAINSTABLE: 'CONTAINSTABLE';
CONTINUE: 'CONTINUE';
CONVERT: 'TRY_'? 'CONVERT';
CREATE: 'CREATE';
CROSS: 'CROSS';
CURRENT: 'CURRENT';
CURRENT_DATE: 'CURRENT_DATE';
CURRENT_TIME: 'CURRENT_TIME';
CURRENT_TIMESTAMP: 'CURRENT_TIMESTAMP';
CURRENT_USER: 'CURRENT_USER';
CURSOR: 'CURSOR';
DATABASE: 'DATABASE';
DBCC: 'DBCC';
DEALLOCATE: 'DEALLOCATE';
DECLARE: 'DECLARE';
DEFAULT: 'DEFAULT';
DELETE: 'DELETE';
DENY: 'DENY';
DESC: 'DESC';
DISK: 'DISK';
DISTINCT: 'DISTINCT';
DISTRIBUTED: 'DISTRIBUTED';
DOUBLE: 'DOUBLE';
DROP: 'DROP';
DUMP: 'DUMP';
ELSE: 'ELSE';
END: 'END';
ERRLVL: 'ERRLVL';
ESCAPE: 'ESCAPE';
EXCEPT: 'EXCEPT';
EXECUTE: 'EXEC' 'UTE'?;
EXISTS: 'EXISTS';
EXIT: 'EXIT';
EXTERNAL: 'EXTERNAL';
FETCH: 'FETCH';
FILE: 'FILE';
FILLFACTOR: 'FILLFACTOR';
FOR: 'FOR';
FOREIGN: 'FOREIGN';
FREETEXT: 'FREETEXT';
FREETEXTTABLE: 'FREETEXTTABLE';
FROM: 'FROM';
FULL: 'FULL';
FUNCTION: 'FUNCTION';
GO: 'GO';
GOTO: 'GOTO';
GRANT: 'GRANT';
GROUP: 'GROUP';
HAVING: 'HAVING';
HOLDLOCK: 'HOLDLOCK';
IDENTITY: 'IDENTITY';
IDENTITY_INSERT: 'IDENTITY_INSERT';
IDENTITYCOL: 'IDENTITYCOL';
IF: 'IF';
IN: 'IN';
INDEX: 'INDEX';
INNER: 'INNER';
INSERT: 'INSERT';
INTERSECT: 'INTERSECT';
INTO: 'INTO';
IS: 'IS';
JOIN: 'JOIN';
KEY: 'KEY';
KILL: 'KILL';
LEFT: 'LEFT';
LIKE: 'LIKE';
LINENO: 'LINENO';
LOAD: 'LOAD';
MERGE: 'MERGE';
NATIONAL: 'NATIONAL';
NOCHECK: 'NOCHECK';
NONCLUSTERED: 'NONCLUSTERED';
NOT: 'NOT';
NULL: 'NULL';
NULLIF: 'NULLIF';
OF: 'OF';
OFF: 'OFF';
OFFSETS: 'OFFSETS';
ON: 'ON';
OPEN: 'OPEN';
OPENDATASOURCE: 'OPENDATASOURCE';
OPENQUERY: 'OPENQUERY';
OPENROWSET: 'OPENROWSET';
OPENXML: 'OPENXML';
OPTION: 'OPTION';
OR: 'OR';
ORDER: 'ORDER';
OUTER: 'OUTER';
OVER: 'OVER';
PERCENT: 'PERCENT';
PIVOT: 'PIVOT';
PLAN: 'PLAN';
PRECISION: 'PRECISION';
PRIMARY: 'PRIMARY';
PRINT: 'PRINT';
PROC: 'PROC';
PROCEDURE: 'PROCEDURE';
PUBLIC: 'PUBLIC';
PYTHON: 'PYTHON';
RAISERROR: 'RAISERROR';
READ: 'READ';
READTEXT: 'READTEXT';
RECONFIGURE: 'RECONFIGURE';
REFERENCES: 'REFERENCES';
REPLICATION: 'REPLICATION';
RESTORE: 'RESTORE';
RESTRICT: 'RESTRICT';
RETURN: 'RETURN';
REVERT: 'REVERT';
REVOKE: 'REVOKE';
RIGHT: 'RIGHT';
ROLLBACK: 'ROLLBACK';
ROWCOUNT: 'ROWCOUNT';
ROWGUIDCOL: 'ROWGUIDCOL';
RULE: 'RULE';
SAVE: 'SAVE';
SCHEMA: 'SCHEMA';
SECURITYAUDIT: 'SECURITYAUDIT';
SELECT: 'SELECT';
SEMANTICKEYPHRASETABLE: 'SEMANTICKEYPHRASETABLE';
SEMANTICSIMILARITYDETAILSTABLE: 'SEMANTICSIMILARITYDETAILSTABLE';
SEMANTICSIMILARITYTABLE: 'SEMANTICSIMILARITYTABLE';
SESSION_USER: 'SESSION_USER';
SET: 'SET';
SETUSER: 'SETUSER';
SHUTDOWN: 'SHUTDOWN';
SOME: 'SOME';
STATISTICS: 'STATISTICS';
SYSTEM_USER: 'SYSTEM_USER';
TABLE: 'TABLE';
TABLESAMPLE: 'TABLESAMPLE';
TEXTSIZE: 'TEXTSIZE';
THEN: 'THEN';
TO: 'TO';
TOP: 'TOP';
TRAN: 'TRAN';
TRANSACTION: 'TRANSACTION';
TRIGGER: 'TRIGGER';
TRUNCATE: 'TRUNCATE';
TRY_CONVERT: 'TRY_CONVERT';
TSEQUAL: 'TSEQUAL';
UNION: 'UNION';
UNIQUE: 'UNIQUE';
UNPIVOT: 'UNPIVOT';
UPDATE: 'UPDATE';
UPDATETEXT: 'UPDATETEXT';
USE: 'USE';
USER: 'USER';
VALUES: 'VALUES';
VARYING: 'VARYING';
VIEW: 'VIEW';
WAITFOR: 'WAITFOR';
WHEN: 'WHEN';
WHERE: 'WHERE';
WHILE: 'WHILE';
WINDOW : 'WINDOW';
WITH: 'WITH';
WITHIN_GROUP: 'WITHIN GROUP';
WRITETEXT: 'WRITETEXT';

// Additional keywords. They can be id, please keep them in sync with the parser.
ABORT: 'ABORT';  // first identifier rule, sync with CustomTSQLAntlrErrorStrategy
ABORT_AFTER_WAIT: 'ABORT_AFTER_WAIT';
ABSENT: 'ABSENT';
ABSOLUTE: 'ABSOLUTE';
ACCENT_SENSITIVITY: 'ACCENT_SENSITIVITY';
ACCESS: 'ACCESS';
ACTION: 'ACTION';
ACTIVATION: 'ACTIVATION';
ACTIVE: 'ACTIVE';
ADDRESS: 'ADDRESS';
ADMINISTER: 'ADMINISTER';
AES: 'AES';
AES_128: 'AES_128';
AES_192: 'AES_192';
AES_256: 'AES_256';
AFFINITY: 'AFFINITY';
AFTER: 'AFTER';
AGGREGATE: 'AGGREGATE';
ALGORITHM: 'ALGORITHM';
ALLOW_CONNECTIONS: 'ALLOW_CONNECTIONS';
ALLOW_ENCRYPTED_VALUE_MODIFICATIONS: 'ALLOW_ENCRYPTED_VALUE_MODIFICATIONS';
ALLOW_MULTIPLE_EVENT_LOSS: 'ALLOW_MULTIPLE_EVENT_LOSS';
ALLOW_SINGLE_EVENT_LOSS: 'ALLOW_SINGLE_EVENT_LOSS';
ALLOW_SNAPSHOT_ISOLATION: 'ALLOW_SNAPSHOT_ISOLATION';
ALLOWED: 'ALLOWED';
ALL_SPARSE_COLUMNS : 'ALL_SPARSE_COLUMNS';
ALWAYS: 'ALWAYS';
ANONYMOUS: 'ANONYMOUS';
ANSI_NULL_DEFAULT: 'ANSI_NULL_DEFAULT';
ANSI_NULLS: 'ANSI_NULLS';
ANSI_PADDING: 'ANSI_PADDING';
ANSI_WARNINGS: 'ANSI_WARNINGS';
APPEND: 'APPEND';
APPEND_ONLY: 'APPEND_ONLY';
APPLICATION: 'APPLICATION';
APPLICATION_LOG: 'APPLICATION_LOG';
APPLY: 'APPLY';
APPROX_PERCENTILE_CONT: 'APPROX_PERCENTILE_CONT';
APPROX_PERCENTILE_DISC: 'APPROX_PERCENTILE_DISC';
ARITHABORT: 'ARITHABORT';
ASSEMBLY: 'ASSEMBLY';
ASYMMETRIC: 'ASYMMETRIC';
ASYNCHRONOUS_COMMIT: 'ASYNCHRONOUS_COMMIT';
AUDIT: 'AUDIT';
AUDIT_GUID: 'AUDIT_GUID';
AUTHENTICATE: 'AUTHENTICATE';
AUTHENTICATION: 'AUTHENTICATION';
AUTO: 'AUTO';
AUTO_CLEANUP: 'AUTO_CLEANUP';
AUTO_CLOSE: 'AUTO_CLOSE';
AUTO_CREATE_STATISTICS: 'AUTO_CREATE_STATISTICS';
AUTO_DROP : 'AUTO_DROP';
AUTO_SHRINK: 'AUTO_SHRINK';
AUTO_UPDATE_STATISTICS: 'AUTO_UPDATE_STATISTICS';
AUTO_UPDATE_STATISTICS_ASYNC: 'AUTO_UPDATE_STATISTICS_ASYNC';
AUTOGROW_ALL_FILES: 'AUTOGROW_ALL_FILES';
AUTOGROW_SINGLE_FILE: 'AUTOGROW_SINGLE_FILE';
AUTOMATED_BACKUP_PREFERENCE: 'AUTOMATED_BACKUP_PREFERENCE';
AUTOMATIC: 'AUTOMATIC';
AVAILABILITY: 'AVAILABILITY';
AVAILABILITY_MODE: 'AVAILABILITY_MODE';
BACKUP_PRIORITY: 'BACKUP_PRIORITY';
BEFORE: 'BEFORE';
BEGIN_DIALOG: 'BEGIN_DIALOG';
BINARY_BASE64: 'BINARY BASE64';
BINDING: 'BINDING';
BLOB_STORAGE: 'BLOB_STORAGE';
BLOCK: 'BLOCK';
BLOCKERS: 'BLOCKERS';
BLOCKING_HIERARCHY: 'BLOCKING_HIERARCHY';
BLOCKSIZE: 'BLOCKSIZE';
BOUNDING_BOX: 'BOUNDING_BOX';
BROKER: 'BROKER';
BROKER_INSTANCE: 'BROKER_INSTANCE';
BUFFER: 'BUFFER';
BUFFERCOUNT: 'BUFFERCOUNT';
BULK_LOGGED: 'BULK_LOGGED';
CACHE: 'CACHE';
CALLED: 'CALLED';
CALLER: 'CALLER';
CAP_CPU_PERCENT: 'CAP_CPU_PERCENT';
CAST: 'TRY_'? 'CAST';
CATALOG: 'CATALOG';
CATCH: 'CATCH';
CERTIFICATE: 'CERTIFICATE';
CHANGE: 'CHANGE';
CHANGE_RETENTION: 'CHANGE_RETENTION';
CHANGE_TRACKING: 'CHANGE_TRACKING';
CHANGES: 'CHANGES';
CHANGETABLE: 'CHANGETABLE';
CHAR: 'CHAR';
CHARACTER: 'CHARACTER';
CHECK_EXPIRATION: 'CHECK_EXPIRATION';
CHECK_POLICY: 'CHECK_POLICY';
CHECKSUM: 'CHECKSUM';
CLASSIFIER_FUNCTION: 'CLASSIFIER_FUNCTION';
CLEANUP: 'CLEANUP';
CLUSTER: 'CLUSTER';
COLLECTION: 'COLLECTION';
COLUMNSTORE: 'COLUMNSTORE';
COLUMN_ENCRYPTION_KEY:  'COLUMN_ENCRYPTION_KEY';
COLUMN_MASTER_KEY: 'COLUMN_MASTER_KEY';
COLUMN_SET: 'COLUMN_SET';
COMMITTED: 'COMMITTED';
COMPATIBILITY_LEVEL: 'COMPATIBILITY_LEVEL';
COMPRESSION: 'COMPRESSION';
CONCAT: 'CONCAT';
CONCAT_NULL_YIELDS_NULL: 'CONCAT_NULL_YIELDS_NULL';
CONFIGURATION: 'CONFIGURATION';
CONNECT: 'CONNECT';
CONNECTION: 'CONNECTION';
CONNECTION_OPTIONS : 'CONNECTION_OPTIONS';
CONTAINMENT: 'CONTAINMENT';
CONTENT: 'CONTENT';
CONTEXT: 'CONTEXT';
CONTINUE_AFTER_ERROR: 'CONTINUE_AFTER_ERROR';
CONTRACT: 'CONTRACT';
CONTRACT_NAME: 'CONTRACT_NAME';
CONTROL: 'CONTROL';
CONVERSATION: 'CONVERSATION';
COOKIE: 'COOKIE';
COPY_ONLY: 'COPY_ONLY';
COUNTER: 'COUNTER';
CPU: 'CPU';
CREATE_NEW: 'CREATE_NEW';
CREATION_DISPOSITION: 'CREATION_DISPOSITION';
CREDENTIAL: 'CREDENTIAL';
CRYPTOGRAPHIC: 'CRYPTOGRAPHIC';
CUBE: 'CUBE';
CURSOR_CLOSE_ON_COMMIT: 'CURSOR_CLOSE_ON_COMMIT';
CURSOR_DEFAULT: 'CURSOR_DEFAULT';
CYCLE: 'CYCLE';
DATA: 'DATA';
DATA_CONSISTENCY_CHECK: 'DATA_CONSISTENCY_CHECK';
DATA_DELETION: 'DATA_DELETION';
DATA_SOURCE: 'DATA_SOURCE';
DATABASE_MIRRORING: 'DATABASE_MIRRORING';
DATABASE_SNAPSHOT: 'DATABASE_SNAPSHOT';
DATASPACE: 'DATASPACE';
DATE_CORRELATION_OPTIMIZATION: 'DATE_CORRELATION_OPTIMIZATION';
DAYS: 'DAYS';
DATE_FORMAT : 'DATE_FORMAT';
DAY: 'DAY';
DBNAME: 'DBNAME';
DB_CHAINING: 'DB_CHAINING';
DB_FAILOVER: 'DB_FAILOVER';
DDL: 'DDL';
DECRYPTION: 'DECRYPTION';
DEFAULT_DATABASE: 'DEFAULT_DATABASE';
DEFAULT_FULLTEXT_LANGUAGE: 'DEFAULT_FULLTEXT_LANGUAGE';
DEFAULT_LANGUAGE: 'DEFAULT_LANGUAGE';
DEFAULT_SCHEMA: 'DEFAULT_SCHEMA';
DEFINITION: 'DEFINITION';
DELAY: 'DELAY';
DELAYED_DURABILITY: 'DELAYED_DURABILITY';
DEPENDENTS: 'DEPENDENTS';
DES: 'DES';
DESCRIPTION: 'DESCRIPTION';
DESX: 'DESX';
DETERMINISTIC: 'DETERMINISTIC';
DHCP: 'DHCP';
DIAGNOSTICS: 'DIAGNOSTICS';
DIALOG: 'DIALOG';
DIFFERENTIAL: 'DIFFERENTIAL';
DIRECTORY_NAME: 'DIRECTORY_NAME';
DISABLE: 'DISABLE';
DISABLE_BROKER: 'DISABLE_BROKER';
DISABLED: 'DISABLED';
DISTRIBUTED_AGG: 'DISTRIBUTED_AGG';
DOCUMENT: 'DOCUMENT';
DTC_SUPPORT: 'DTC_SUPPORT';
DYNAMIC: 'DYNAMIC';
ELEMENTS: 'ELEMENTS';
EMERGENCY: 'EMERGENCY';
EMPTY: 'EMPTY';
ENABLE: 'ENABLE';
ENABLE_BROKER: 'ENABLE_BROKER';
ENABLED: 'ENABLED';
ENCODING: 'ENCODING';
ENCLAVE_COMPUTATIONS: 'ENCLAVE_COMPUTATIONS';
ENCRYPTED: 'ENCRYPTED';
ENCRYPTED_VALUE: 'ENCRYPTED_VALUE';
ENCRYPTION: 'ENCRYPTION';
ENCRYPTION_TYPE: 'ENCRYPTION_TYPE';
ENDPOINT: 'ENDPOINT';
ENDPOINT_URL: 'ENDPOINT_URL';
ENVIRONMENT_VARIABLES: 'ENVIRONMENT_VARIABLES';
ERROR: 'ERROR';
ERROR_BROKER_CONVERSATIONS: 'ERROR_BROKER_CONVERSATIONS';
EVENT: 'EVENT';
EVENT_RETENTION_MODE: 'EVENT_RETENTION_MODE';
EXCLUSIVE: 'EXCLUSIVE';
EXECUTABLE: 'EXECUTABLE';
EXECUTABLE_FILE: 'EXECUTABLE_FILE';
EXPAND: 'EXPAND';
EXPIREDATE: 'EXPIREDATE';
EXPIRY_DATE: 'EXPIRY_DATE';
EXPLICIT: 'EXPLICIT';
EXTENSION: 'EXTENSION';
EXTERNAL_ACCESS: 'EXTERNAL_ACCESS';
EXTERNAL_MONITOR: 'EXTERNAL_MONITOR';
FAIL_OPERATION: 'FAIL_OPERATION';
FAILOVER: 'FAILOVER';
FAILOVER_MODE: 'FAILOVER_MODE';
FAILURE_CONDITION_LEVEL: 'FAILURE_CONDITION_LEVEL';
FAILURECONDITIONLEVEL: 'FAILURECONDITIONLEVEL';
FALSE: 'FALSE';
FAN_IN: 'FAN_IN';
FAST: 'FAST';
FAST_FORWARD: 'FAST_FORWARD';
FIELD_TERMINATOR: 'FIELD_TERMINATOR';
FILE_SNAPSHOT: 'FILE_SNAPSHOT';
FILEGROUP: 'FILEGROUP';
FILEGROWTH: 'FILEGROWTH';
FILENAME: 'FILENAME';
FILE_NAME: 'FILE_NAME';
FILEPATH: 'FILEPATH';
FILESTREAM: 'FILESTREAM';
FILESTREAM_ON: 'FILESTREAM_ON';
FILELISTONLY: 'FILELISTONLY';
FILTER: 'FILTER';
FILTER_COLUMN: 'FILTER_COLUMN';
FILTER_PREDICATE: 'FILTER_PREDICATE';
FIRST: 'FIRST';
FIRST_ROW: 'FIRST_ROW';
FOLLOWING: 'FOLLOWING';
FORCE: 'FORCE';
FORCE_FAILOVER_ALLOW_DATA_LOSS: 'FORCE_FAILOVER_ALLOW_DATA_LOSS';
FORCE_SERVICE_ALLOW_DATA_LOSS: 'FORCE_SERVICE_ALLOW_DATA_LOSS';
FORCED: 'FORCED';
FORCESEEK: 'FORCESEEK';
FORCESCAN: 'FORCESCAN';
FORMAT: 'FORMAT';
FORMAT_OPTIONS: 'FORMAT_OPTIONS';
FORMAT_TYPE: 'FORMAT_TYPE';
FORWARD_ONLY: 'FORWARD_ONLY';
FULLSCAN: 'FULLSCAN';
FULLTEXT: 'FULLTEXT';
GB: 'GB';
GENERATED: 'GENERATED';
GET: 'GET';
GLOBAL: 'GLOBAL';
GOVERNOR: 'GOVERNOR';
GRIDS: 'GRIDS';
GROUP_MAX_REQUESTS: 'GROUP_MAX_REQUESTS';
GROUPING: 'GROUPING';
HADR: 'HADR';
HASH: 'HASH';
HASHED: 'HASHED';
HEADERONLY: 'HEADERONLY';
HEALTH_CHECK_TIMEOUT: 'HEALTH_CHECK_TIMEOUT';
HEALTHCHECKTIMEOUT: 'HEALTHCHECKTIMEOUT';
HIGH: 'HIGH';
HIDDEN_KEYWORD: 'HIDDEN';
HISTORY_RETENTION_PERIOD: 'HISTORY_RETENTION_PERIOD';
HONOR_BROKER_PRIORITY: 'HONOR_BROKER_PRIORITY';
HOURS: 'HOURS';
HISTORY_TABLE: 'HISTORY_TABLE';
IDENTITY_VALUE: 'IDENTITY_VALUE';
IGNORE_CONSTRAINTS: 'IGNORE_CONSTRAINTS';
IGNORE_NONCLUSTERED_COLUMNSTORE_INDEX: 'IGNORE_NONCLUSTERED_COLUMNSTORE_INDEX';
IGNORE_TRIGGERS: 'IGNORE_TRIGGERS';
IIF: 'IIF';
IMMEDIATE: 'IMMEDIATE';
IMPERSONATE: 'IMPERSONATE';
IMPORTANCE: 'IMPORTANCE';
INBOUND: 'INBOUND';
INCLUDE: 'INCLUDE';
INCLUDE_NULL_VALUES: 'INCLUDE_NULL_VALUES';
INCREMENT: 'INCREMENT';
INCREMENTAL: 'INCREMENTAL';
INFINITE: 'INFINITE';
INIT: 'INIT';
INITIATOR: 'INITIATOR';
INLINE: 'INLINE';
INPUT: 'INPUT';
INSENSITIVE: 'INSENSITIVE';
INSTEAD: 'INSTEAD';
IO: 'IO';
IP: 'IP';
ISOLATION: 'ISOLATION';
JSON: 'JSON';
JSON_ARRAY: 'JSON_ARRAY';
JSON_OBJECT: 'JSON_OBJECT';
KB: 'KB';
KEEP: 'KEEP';
KEEPFIXED: 'KEEPFIXED';
KEEP_CDC: 'KEEP_CDC';
KEEP_REPLICATION: 'KEEP_REPLICATION';
KERBEROS: 'KERBEROS';
KEY_PATH: 'KEY_PATH';
KEY_SOURCE: 'KEY_SOURCE';
KEY_STORE_PROVIDER_NAME: 'KEY_STORE_PROVIDER_NAME';
KEYS: 'KEYS';
KEYSET: 'KEYSET';
LABELONLY: 'LABELONLY';
LANGUAGE: 'LANGUAGE';
LAST: 'LAST';
LAST_NODE: 'LAST_NODE';
LEDGER: 'LEDGER';
LEDGER_VIEW: 'LEDGER_VIEW';
LEVEL: 'LEVEL';
LIBRARY: 'LIBRARY';
LIFETIME: 'LIFETIME';
LINKED: 'LINKED';
LINUX: 'LINUX';
LIST: 'LIST';
LISTENER: 'LISTENER';
LISTENER_IP: 'LISTENER_IP';
LISTENER_PORT: 'LISTENER_PORT';
LISTENER_URL: 'LISTENER_URL';
LOADHISTORY: 'LOADHISTORY';
LOB_COMPACTION: 'LOB_COMPACTION';
LOCAL: 'LOCAL';
LOCAL_SERVICE_NAME: 'LOCAL_SERVICE_NAME';
LOCATION: 'LOCATION';
LOCK: 'LOCK';
LOCK_ESCALATION: 'LOCK_ESCALATION';
LOG: 'LOG';
LOGIN: 'LOGIN';
LOOP: 'LOOP';
LOW: 'LOW';
MANUAL: 'MANUAL';
MARK: 'MARK';
MASK: 'MASK';
MASKED: 'MASKED';
MASTER: 'MASTER';
MATCH: 'MATCH';
MATCHED: 'MATCHED';
MATERIALIZED: 'MATERIALIZED';
MAX: 'MAX';
MAX_CPU_PERCENT: 'MAX_CPU_PERCENT';
MAX_DISPATCH_LATENCY: 'MAX_DISPATCH_LATENCY';
MAX_DOP: 'MAX_DOP';
MAX_DURATION: 'MAX_DURATION';
MAX_EVENT_SIZE: 'MAX_EVENT_SIZE';
MAX_FILES: 'MAX_FILES';
MAX_IOPS_PER_VOLUME: 'MAX_IOPS_PER_VOLUME';
MAX_MEMORY: 'MAX_MEMORY';
MAX_MEMORY_PERCENT: 'MAX_MEMORY_PERCENT';
MAX_OUTSTANDING_IO_PER_VOLUME: 'MAX_OUTSTANDING_IO_PER_VOLUME';
MAX_PROCESSES: 'MAX_PROCESSES';
MAX_QUEUE_READERS: 'MAX_QUEUE_READERS';
MAX_ROLLOVER_FILES: 'MAX_ROLLOVER_FILES';
MAX_SIZE: 'MAX_SIZE';
MAXDOP: 'MAXDOP';
MAXRECURSION: 'MAXRECURSION';
MAXSIZE: 'MAXSIZE';
MAXTRANSFER: 'MAXTRANSFER';
MAXTRANSFERSIZE: 'MAXTRANSFERSIZE';
MAXVALUE: 'MAXVALUE';
MB: 'MB';
MEDIADESCRIPTION: 'MEDIADESCRIPTION';
MEDIANAME: 'MEDIANAME';
MEDIAPASSWORD: 'MEDIAPASSWORD';
MEDIUM: 'MEDIUM';
MEMBER: 'MEMBER';
MEMORY_OPTIMIZED: 'MEMORY_OPTIMIZED';
MEMORY_OPTIMIZED_DATA: 'MEMORY_OPTIMIZED_DATA';
MEMORY_OPTIMIZED_ELEVATE_TO_SNAPSHOT: 'MEMORY_OPTIMIZED_ELEVATE_TO_SNAPSHOT';
MEMORY_PARTITION_MODE: 'MEMORY_PARTITION_MODE';
MESSAGE: 'MESSAGE';
MESSAGE_FORWARD_SIZE: 'MESSAGE_FORWARD_SIZE';
MESSAGE_FORWARDING: 'MESSAGE_FORWARDING';
METADATA_ONLY: 'METADATA_ONLY';
MIGRATION_STATE: 'MIGRATION_STATE';
MIN_ACTIVE_ROWVERSION: 'MIN_ACTIVE_ROWVERSION';
MIN_CPU_PERCENT: 'MIN_CPU_PERCENT';
MIN_IOPS_PER_VOLUME: 'MIN_IOPS_PER_VOLUME';
MIN_MEMORY_PERCENT: 'MIN_MEMORY_PERCENT';
MINUTES: 'MINUTES';
MINVALUE: 'MINVALUE';
MIRROR: 'MIRROR';
MIRROR_ADDRESS: 'MIRROR_ADDRESS';
MIXED_PAGE_ALLOCATION: 'MIXED_PAGE_ALLOCATION';
MODE: 'MODE';
MODIFY: 'MODIFY';
MONTH: 'MONTH';
MONTHS: 'MONTHS';
MOVE: 'MOVE';
MULTI_USER: 'MULTI_USER';
MUST_CHANGE: 'MUST_CHANGE';
NAME: 'NAME';
NCHAR: 'NCHAR';
NEGOTIATE: 'NEGOTIATE';
NESTED_TRIGGERS: 'NESTED_TRIGGERS';
NEW_ACCOUNT: 'NEW_ACCOUNT';
NEW_BROKER: 'NEW_BROKER';
NEW_PASSWORD: 'NEW_PASSWORD';
NEWNAME: 'NEWNAME';
NEXT: 'NEXT';
NO: 'NO';
NO_CHECKSUM: 'NO_CHECKSUM';
NO_COMPRESSION: 'NO_COMPRESSION';
NO_EVENT_LOSS: 'NO_EVENT_LOSS';
NO_TRUNCATE: 'NO_TRUNCATE';
NO_WAIT: 'NO_WAIT';
NODES: 'NODES';
NOEXPAND: 'NOEXPAND';
NOFORMAT: 'NOFORMAT';
NOINIT: 'NOINIT';
NON_TRANSACTED_ACCESS: 'NON_TRANSACTED_ACCESS';
NONE: 'NONE';
NOLOCK: 'NOLOCK';
NONLOAD: 'NONLOAD';
NORECOMPUTE: 'NORECOMPUTE';
NORECOVERY: 'NORECOVERY';
NOREWIND: 'NOREWIND';
NOSKIP: 'NOSKIP';
NOTIFICATION: 'NOTIFICATION';
NOTIFICATIONS: 'NOTIFICATIONS';
NOUNLOAD: 'NOUNLOAD';
NOWAIT: 'NOWAIT';
NTLM: 'NTLM';
NUMANODE: 'NUMANODE';
NUMERIC_ROUNDABORT: 'NUMERIC_ROUNDABORT';
OBJECT: 'OBJECT';
OFFLINE: 'OFFLINE';
OFFSET: 'OFFSET';
OLD_ACCOUNT: 'OLD_ACCOUNT';
OLD_PASSWORD: 'OLD_PASSWORD';
ON_FAILURE: 'ON_FAILURE';
ONLINE: 'ONLINE';
ONLY: 'ONLY';
OPEN_EXISTING: 'OPEN_EXISTING';
OPENJSON: 'OPENJSON';
OPERATIONS: 'OPERATIONS';
OPERATION_TYPE_COLUMN_NAME: 'OPERATION_TYPE_COLUMN_NAME';
OPERATION_TYPE_DESC_COLUMN_NAME: 'OPERATION_TYPE_DESC_COLUMN_NAME';
OPERATOR_AUDIT: 'OPERATOR_AUDIT';
OPTIMISTIC: 'OPTIMISTIC';
OPTIMIZE: 'OPTIMIZE';
OUT: 'OUT';
OUTBOUND: 'OUTBOUND';
OUTPUT: 'OUTPUT';
OVERRIDE: 'OVERRIDE';
OWNER: 'OWNER';
OWNERSHIP: 'OWNERSHIP';
PAGE: 'PAGE';
PAGE_VERIFY: 'PAGE_VERIFY';
PAGLOCK: 'PAGLOCK';
PARAMETERS: 'PARAMETERS';
PARAMETERIZATION: 'PARAMETERIZATION';
PARSE: 'TRY_'? 'PARSE';
PARSER_VERSION: 'PARSER_VERSION';
PARTIAL: 'PARTIAL';
PARTITION: 'PARTITION';
PARTITIONS: 'PARTITIONS';
PARTNER: 'PARTNER';
PASSWORD: 'PASSWORD';
PATH: 'PATH';
PAUSE: 'PAUSE';
PAUSED: 'PAUSED';
PER_CPU: 'PER_CPU';
PER_DB: 'PER_DB';
PER_NODE: 'PER_NODE';
PERCENTILE_CONT: 'PERCENTILE_CONT';
PERCENTILE_DISC: 'PERCENTILE_DISC';
PERIOD: 'PERIOD';
PERMISSION_SET: 'PERMISSION_SET';
PERSIST_SAMPLE_PERSENT: 'PERSIST_SAMPLE_PERSENT';
PERSISTED: 'PERSISTED';
PERSISTENT_LOG_BUFFER: 'PERSISTENT_LOG_BUFFER';
PLATFORM: 'PLATFORM';
POISON_MESSAGE_HANDLING: 'POISON_MESSAGE_HANDLING';
POLICY: 'POLICY';
POOL: 'POOL';
POPULATION: 'POPULATION';
PORT: 'PORT';
PRECEDING: 'PRECEDING';
PREDICATE: 'PREDICATE';
PRIMARY_ROLE: 'PRIMARY_ROLE';
PRIOR: 'PRIOR';
PRIORITY: 'PRIORITY';
PRIORITY_LEVEL: 'PRIORITY_LEVEL';
PRIVATE: 'PRIVATE';
PRIVATE_KEY: 'PRIVATE_KEY';
PRIVILEGES: 'PRIVILEGES';
PROCEDURE_NAME: 'PROCEDURE_NAME';
PROCESS: 'PROCESS';
PROFILE: 'PROFILE';
PROPERTY: 'PROPERTY';
PROPERTY_DESCRIPTION: 'PROPERTY_DESCRIPTION';
PROPERTY_INT_ID: 'PROPERTY_INT_ ID';
PROPERTY_SET_GUID: 'PROPERTY_SET_ GUID';
PROVIDER: 'PROVIDER';
PROVIDER_KEY_NAME: 'PROVIDER_KEY_NAME';
PUSHDOWN: 'PUSHDOWN';
RANDOMIZED: 'RANDOMIZED';
READCOMMITTED: 'READCOMMITTED';
READCOMMITTEDLOCK: 'READCOMMITTEDLOCK';
READPAST: 'READPAST';
READUNCOMMITTED: 'READUNCOMMITTED';
REMOTE_DATA_ARCHIVE: 'REMOTE_DATA_ARCHIVE';
REPEATABLEREAD: 'REPEATABLEREAD';
RETENTION_PERIOD: 'RETENTION_PERIOD';
REWINDONLY: 'REWINDONLY';
ROWLOCK: 'ROWLOCK';
QUERY: 'QUERY';
QUERYTRACEON: 'QUERYTRACEON';
QUEUE: 'QUEUE';
QUEUE_DELAY: 'QUEUE_DELAY';
QUOTED_IDENTIFIER: 'QUOTED_IDENTIFIER';
R_LETTER: 'R';
RANGE: 'RANGE';
RAW: 'RAW';
RC2: 'RC2';
RC4: 'RC4';
RC4_128: 'RC4_128';
READ_COMMITTED_SNAPSHOT: 'READ_COMMITTED_SNAPSHOT';
READ_ONLY: 'READ_ONLY';
READ_ONLY_ROUTING_LIST: 'READ_ONLY_ROUTING_LIST';
READ_ONLY_ROUTING_URL : 'READ_ONLY_ROUTING_URL';
READ_WRITE: 'READ_WRITE';
READ_WRITE_FILEGROUPS: 'READ_WRITE_FILEGROUPS';
READ_WRITE_ROUTING_URL: 'READ_WRITE_ROUTING_URL';
READONLY: 'READONLY';
READWRITE: 'READWRITE';
REBUILD: 'REBUILD';
RECEIVE: 'RECEIVE';
RECOMPILE: 'RECOMPILE';
RECOVERY: 'RECOVERY';
RECURSIVE_TRIGGERS: 'RECURSIVE_TRIGGERS';
REGENERATE: 'REGENERATE';
RELATED_CONVERSATION: 'RELATED_CONVERSATION';
RELATED_CONVERSATION_GROUP: 'RELATED_CONVERSATION_GROUP';
RELATIVE: 'RELATIVE';
REMOTE: 'REMOTE';
REMOTE_SERVICE_NAME: 'REMOTE_SERVICE_NAME';
REMOVE: 'REMOVE';
REORGANIZE: 'REORGANIZE';
REPEATABLE: 'REPEATABLE';
REPLACE: 'REPLACE';
REPLICA: 'REPLICA';
REQUEST_MAX_CPU_TIME_SEC: 'REQUEST_MAX_CPU_TIME_SEC';
REQUEST_MAX_MEMORY_GRANT_PERCENT: 'REQUEST_MAX_MEMORY_GRANT_PERCENT';
REQUEST_MEMORY_GRANT_TIMEOUT_SEC: 'REQUEST_MEMORY_GRANT_TIMEOUT_SEC';
REQUIRED: 'REQUIRED';
REQUIRED_SYNCHRONIZED_SECONDARIES_TO_COMMIT: 'REQUIRED_SYNCHRONIZED_SECONDARIES_TO_COMMIT';
RESAMPLE: 'RESAMPLE';
RESERVE_DISK_SPACE: 'RESERVE_DISK_SPACE';
RESET: 'RESET';
RESOURCE: 'RESOURCE';
RESOURCE_MANAGER_LOCATION: 'RESOURCE_MANAGER_LOCATION';
RESOURCES: 'RESOURCES';
RESTART: 'RESTART';
RESTRICTED_USER: 'RESTRICTED_USER';
RESULT: 'RESULT';
RESUME: 'RESUME';
RETAINDAYS: 'RETAINDAYS';
RETENTION: 'RETENTION';
RETURNS: 'RETURNS';
REWIND: 'REWIND';
ROBUST: 'ROBUST';
ROLE: 'ROLE';
ROLLUP: 'ROLLUP';
ROOT: 'ROOT';
ROUTE: 'ROUTE';
ROW: 'ROW';
ROWGUID: 'ROWGUID';
ROWS: 'ROWS';
RSA_1024: 'RSA_1024';
RSA_2048: 'RSA_2048';
RSA_3072: 'RSA_3072';
RSA_4096: 'RSA_4096';
RSA_512: 'RSA_512';
SAFE: 'SAFE';
SAFETY: 'SAFETY';
SAMPLE: 'SAMPLE';
SCHEDULER: 'SCHEDULER';
SCHEMABINDING: 'SCHEMABINDING';
SCHEME: 'SCHEME';
SCOPED: 'SCOPED';
SCRIPT: 'SCRIPT';
SCROLL: 'SCROLL';
SCROLL_LOCKS: 'SCROLL_LOCKS';
SEARCH: 'SEARCH';
SECONDARY: 'SECONDARY';
SECONDARY_ONLY: 'SECONDARY_ONLY';
SECONDARY_ROLE: 'SECONDARY_ROLE';
SECONDS: 'SECONDS';
SECRET: 'SECRET';
SECURABLES: 'SECURABLES';
SECURITY: 'SECURITY';
SECURITY_LOG: 'SECURITY_LOG';
SEEDING_MODE: 'SEEDING_MODE';
SELECTIVE: 'SELECTIVE';
SELF: 'SELF';
SEMI_SENSITIVE: 'SEMI_SENSITIVE';
SEND: 'SEND';
SENT: 'SENT';
SEQUENCE: 'SEQUENCE';
SEQUENCE_NUMBER: 'SEQUENCE_NUMBER';
SEQUENCE_NUMBER_COLUMN_NAME: 'SEQUENCE_NUMBER_COLUMN_NAME';
SERIALIZABLE: 'SERIALIZABLE';
SERVER: 'SERVER';
SERVICE: 'SERVICE';
SERVICE_BROKER: 'SERVICE_BROKER';
SERVICE_NAME: 'SERVICE_NAME';
SESSION: 'SESSION';
SESSION_TIMEOUT: 'SESSION_TIMEOUT';
SETERROR: 'SETERROR';
SETS: 'SETS';
SETTINGS: 'SETTINGS';
SHARE: 'SHARE';
SHORTEST_PATH: 'SHORTEST_PATH';
SHOWPLAN: 'SHOWPLAN';
SID: 'SID';
SIGNATURE: 'SIGNATURE';
SIMPLE: 'SIMPLE';
SINGLE_USER: 'SINGLE_USER';
SINGLETON: 'SINGLETON';
SIZE: 'SIZE';
SKIP_KEYWORD: 'SKIP_KEYWORD';
SNAPSHOT: 'SNAPSHOT';
SOFTNUMA: 'SOFTNUMA';
SOURCE: 'SOURCE';
SPARSE: 'SPARSE';
SPATIAL: 'SPATIAL';
SPATIAL_WINDOW_MAX_CELLS: 'SPATIAL_WINDOW_MAX_CELLS';
SPECIFICATION: 'SPECIFICATION';
SPLIT: 'SPLIT';
SQL: 'SQL';
SQLDUMPERFLAGS: 'SQLDUMPERFLAGS';
SQLDUMPERPATH: 'SQLDUMPERPATH';
SQLDUMPERTIMEOUT: 'SQLDUMPERTIMEOUT';
STANDBY: 'STANDBY';
START: 'START';
START_DATE: 'START_DATE';
STARTED: 'STARTED';
STARTUP_STATE: 'STARTUP_STATE';
STATE: 'STATE';
STATIC: 'STATIC';
STATISTICAL_SEMANTICS: 'STATISTICAL_SEMANTICS';
STATISTICS_INCREMENTAL: 'STATISTICS_INCREMENTAL';
STATS: 'STATS';
STATUS: 'STATUS';
STOP: 'STOP';
STOPAT: 'STOPAT';
STOPATMARK: 'STOPATMARK';
STOPBEFOREMARK: 'STOPBEFOREMARK';
STOP_ON_ERROR: 'STOP_ON_ERROR';
STOPLIST: 'STOPLIST';
STOPPED: 'STOPPED';
STRING_AGG: 'STRING_AGG';
STRING_DELIMITER: 'STRING_DELIMITER';
SUBJECT: 'SUBJECT';
SUBSCRIBE: 'SUBSCRIBE';
SUPPORTED: 'SUPPORTED';
SUSPEND: 'SUSPEND';
SWITCH: 'SWITCH';
SYMMETRIC: 'SYMMETRIC';
SYNCHRONOUS_COMMIT: 'SYNCHRONOUS_COMMIT';
SYNONYM: 'SYNONYM';
SYSTEM: 'SYSTEM';
SYSTEM_TIME: 'SYSTEM_TIME';
SYSTEM_VERSIONING: 'SYSTEM_VERSIONING';
TABLOCK: 'TABLOCK';
TABLOCKX: 'TABLOCKX';
TAKE: 'TAKE';
TAPE: 'TAPE';
TARGET: 'TARGET';
TARGET_RECOVERY_TIME: 'TARGET_RECOVERY_TIME';
TB: 'TB';
TCP: 'TCP';
TEXTIMAGE_ON: 'TEXTIMAGE_ON';
THROW: 'THROW';
TIES: 'TIES';
TIME: 'TIME';
TIMEOUT: 'TIMEOUT';
TIMER: 'TIMER';
TORN_PAGE_DETECTION: 'TORN_PAGE_DETECTION';
TRACE: 'TRACE';
TRACK_CAUSALITY: 'TRACK_CAUSALITY';
TRACK_COLUMNS_UPDATED: 'TRACK_COLUMNS_UPDATED';
TRACKING: 'TRACKING';
TRANSACTION_ID: 'TRANSACTION_ID';
TRANSACTION_ID_COLUMN_NAME: 'TRANSACTION_ID_COLUMN_NAME';
TRANSFER: 'TRANSFER';
TRANSFORM_NOISE_WORDS: 'TRANSFORM_NOISE_WORDS';
TRIM: 'TRIM';
TRIPLE_DES: 'TRIPLE_DES';
TRIPLE_DES_3KEY: 'TRIPLE_DES_3KEY';
TRUE: 'TRUE';
TRUSTWORTHY: 'TRUSTWORTHY';
TRY: 'TRY';
TSQL: 'TSQL';
TWO_DIGIT_YEAR_CUTOFF: 'TWO_DIGIT_YEAR_CUTOFF';
TYPE: 'TYPE';
TYPE_WARNING: 'TYPE_WARNING';
UNBOUNDED: 'UNBOUNDED';
UNCHECKED: 'UNCHECKED';
UNCOMMITTED: 'UNCOMMITTED';
UNDEFINED: 'UNDEFINED';
UNKNOWN: 'UNKNOWN';
UNLIMITED: 'UNLIMITED';
UNLOAD: 'UNLOAD';
UNLOCK: 'UNLOCK';
UNMASK: 'UNMASK';
UNSAFE: 'UNSAFE';
UPDLOCK: 'UPDLOCK';
URL: 'URL';
USED: 'USED';
USE_TYPE_DEFAULT: 'USE_TYPE_DEFAULT';
USING: 'USING';
VALID_XML: 'VALID_XML';
VALIDATION: 'VALIDATION';
VALUE: 'VALUE';
VERBOSELOGGING: 'VERBOSELOGGING';
VERIFYONLY: 'VERIFYONLY';
VERSION: 'VERSION';
VIEW_METADATA: 'VIEW_METADATA';
VIEWS: 'VIEWS';
VISIBILITY: 'VISIBILITY';
WAIT: 'WAIT';
WAIT_AT_LOW_PRIORITY: 'WAIT_AT_LOW_PRIORITY';
WEEK: 'WEEK';
WEEKS: 'WEEKS';
WELL_FORMED_XML: 'WELL_FORMED_XML';
WINDOWS: 'WINDOWS';
WITHOUT: 'WITHOUT';
WITHOUT_ARRAY_WRAPPER: 'WITHOUT_ARRAY_WRAPPER';
WITNESS: 'WITNESS';
WORK: 'WORK';
WORKLOAD: 'WORKLOAD';
XLOCK: 'XLOCK';
XML: 'XML';
XMLDATA: 'XMLDATA';
XMLNAMESPACES: 'XMLNAMESPACES';
XMLSCHEMA: 'XMLSCHEMA';
XQUERY: 'XQUERY';
XSINIL: 'XSINIL';
YEAR: 'YEAR';
YEARS: 'YEARS'; // last identifier rule, sync with CustomTSQLAntlrErrorStrategy

// Combinations that cannot be used as IDs
DOLLAR_ACTION:      DOLLAR ACTION;
DOLLAR_PARTITION:   DOLLAR PARTITION;

// https://docs.microsoft.com/en-us/sql/t-sql/language-elements/slash-star-comment-transact-sql
COMMENT:            '/*' (COMMENT | .)*? '*/' -> channel(HIDDEN);
LINE_COMMENT:       '--' ~[\r\n]* -> channel(HIDDEN);

// TODO: ID can be not only Latin.
LEFT_FIGURE_PAREN:  '{';
RIGHT_FIGURE_PAREN: '}';
DOUBLE_QUOTE_ID:  UnterminatedQuotedIdentifier '"'
    // unquote so that we may always call getText() and not worry about quotes
        {
            String __tx = getText();
            setText(__tx.substring(1, __tx.length() - 1).replace("\"\"", "\""));
        }
    ;
    
SINGLE_QUOTE:       '\'';
SQUARE_BRACKET_ID:  UnterminatedSquareQuotedIdentifier ']'
    // unquote so that we may always call getText() and not worry about quotes
        {
            String __tx = getText();
            setText(__tx.substring(1, __tx.length() - 1).replace("]]", "]"));
        }
    ;

// https://learn.microsoft.com/en-us/sql/t-sql/language-elements/variables-transact-sql?view=sql-server-ver16
LOCAL_ID:           '@' ([A-Z_#$@0-9] | FullWidthLetter)+;
DECIMAL:             DEC_DIGIT+;
ID:                  ([A-Z_#] | FullWidthLetter) ([A-Z_#$@0-9] | FullWidthLetter)*;
STRING:              'N'? '\'' (~'\'' | '\'\'')* '\'';
BINARY:              '0' 'X' (HEX_DIGIT | BACKSLASH [\r]? [\n])*;
FLOAT:               DEC_DOT_DEC;
REAL:                (DECIMAL | DEC_DOT_DEC) ('E' [+-]? DEC_DIGIT+);

IPV4_ADDR:           IPV4_OCTECT DOT IPV4_OCTECT DOT IPV4_OCTECT DOT IPV4_OCTECT;
// TODO doesn't support IPV6 zero compression
IPV6_ADDR:           HEX_DIGIT? HEX_DIGIT? HEX_DIGIT? HEX_DIGIT [:] HEX_DIGIT? HEX_DIGIT? HEX_DIGIT? HEX_DIGIT [:] 
                     HEX_DIGIT? HEX_DIGIT? HEX_DIGIT? HEX_DIGIT [:] HEX_DIGIT? HEX_DIGIT? HEX_DIGIT? HEX_DIGIT [:]
                     HEX_DIGIT? HEX_DIGIT? HEX_DIGIT? HEX_DIGIT [:] HEX_DIGIT? HEX_DIGIT? HEX_DIGIT? HEX_DIGIT [:]
                     HEX_DIGIT? HEX_DIGIT? HEX_DIGIT? HEX_DIGIT [:] HEX_DIGIT? HEX_DIGIT? HEX_DIGIT? HEX_DIGIT;
                     
BACKSLASH:           '\\';
DOUBLE_FORWARD_SLASH:'//';

EQUAL:               '=';

GREATER:             '>';
LESS:                '<';
EXCLAMATION:         '!';

PLUS_ASSIGN:         '+=';
MINUS_ASSIGN:        '-=';
MULT_ASSIGN:         '*=';
DIV_ASSIGN:          '/=';
MOD_ASSIGN:          '%=';
AND_ASSIGN:          '&=';
XOR_ASSIGN:          '^=';
OR_ASSIGN:           '|=';

DOT:                 '.';
AT:                  '@';
DOLLAR:              '$';
LR_BRACKET:          '(';
RR_BRACKET:          ')';
COMMA:               ',';
SEMI:                ';';
COLON:               ':';
STAR:                '*';
DIVIDE:              '/';
MODULE:              '%';
PLUS:                '+';
MINUS:               '-';
BIT_NOT:             '~';
BIT_OR:              '|';
BIT_AND:             '&';
BIT_XOR:             '^';

// This is a quoted identifier which only contains valid characters but is not terminated
fragment UnterminatedQuotedIdentifier
    : '"'
    ( '""'
    | ~[\u0000"]
    )*
    ;

// This is a quoted identifier which only contains valid characters but is not terminated
fragment UnterminatedSquareQuotedIdentifier
    : '['
    ( ']]'
    | ~[\u0000\]]
    )*
    ;

fragment IPV4_OCTECT:  [0-9]?[0-9]?[0-9];
fragment DEC_DOT_DEC:  (DEC_DIGIT+ '.' DEC_DIGIT+ |  DEC_DIGIT+ '.' | '.' DEC_DIGIT+);
fragment HEX_DIGIT:    [0-9A-F];
fragment DEC_DIGIT:    [0-9];

fragment FullWidthLetter options {
    caseInsensitive = false;
  } : '\u00c0'..'\u00d6'
    | '\u00d8'..'\u00f6'
    | '\u00f8'..'\u00ff'
    | '\u0100'..'\u1fff'
    | '\u2c00'..'\u2fff'
    | '\u3040'..'\u318f'
    | '\u3300'..'\u337f'
    | '\u3400'..'\u3fff'
    | '\u4e00'..'\u9fff'
    | '\ua000'..'\ud7ff'
    | '\uf900'..'\ufaff'
    | '\uff00'..'\ufff0'
    // | '\u10000'..'\u1F9FF'  //not support four bytes chars
    // | '\u20000'..'\u2FA1F'
    ;

BOM: '\ufeff';
SPACE:              [ \t\r\n]+    -> skip;
ZERO_LENGHT_SPACE: '\u200B'..'\u200F' -> skip;

BAD
  : .
  ;
